# Sistema IoT para Detecci√≥n de Incendios en los Cerros Orientales de Bogot√° - Challenge 3

## Informaci√≥n del Proyecto
- **Universidad:** Universidad de La Sabana  
- **Facultad:** Facultad de Ingenier√≠a  
- **Materia:** Internet de las Cosas  
- **Profesor:** Juan Manuel Aranda L√≥pez King  

## Integrantes del Proyecto
| Nombre | Correo Electr√≥nico |
|--------|-------------------|
| Valentina Alejandra L√≥pez Romero | valentinalopro@unisabana.edu.co |
| Ana Luc√≠a Quintero Vargas | anaquiva@unisabana.edu.co |
| Mariana Valle Moreno | marianavamo@unisabana.edu.co |

## Estructura de la Documentaci√≥n
- [1. Introducci√≥n](#1-introducci√≥n)
- [2. Motivaci√≥n y Justificaci√≥n](#2-motivaci√≥n-y-justificaci√≥n)
- [3. Soluci√≥n Propuesta](#3-soluci√≥n-propuesta)
- [4. Configuraci√≥n Experimental, Resultados y An√°lisis](#4-configuraci√≥n-experimental-resultados-y-an√°lisis)
- [5. Autoevaluaci√≥n del Protocolo de Pruebas](#5-autoevaluaci√≥n-del-protocolo-de-pruebas)
- [6. Conclusiones, Retos y Mejoras Futuras](#6-conclusiones-retos-y-mejoras-futuras)
- [7. Referencias](#7-referencias)
- [8. Anexos](#8-anexos)

---

## 1. Introducci√≥n
<p align="justify">
Los cerros orientales de Bogot√° son fundamentales para la regulaci√≥n clim√°tica y la conservaci√≥n de la biodiversidad. Su presencia como barrera y protector natural constituye un regulador del clima, del cual depende en buena medida la disponibilidad de agua para la capital y municipios aleda√±os [1]. Adem√°s, son esenciales en la producci√≥n de ox√≠geno en una sabana donde la p√©rdida de vegetaci√≥n es creciente, lo que los hace a√∫n m√°s vulnerables a incendios forestales agravados por sequ√≠as prolongadas, altas temperaturas y la acumulaci√≥n de material seco, junto con actividades humanas como fogatas, quemas agr√≠colas y expansi√≥n urbana descontrolada [2].
</p>

<p align="justify">
Ante este panorama, es fundamental implementar sistemas inteligentes de monitoreo ambiental que permitan la detecci√≥n temprana de incendios y habiliten una respuesta efectiva por parte de las autoridades. Este proyecto propone el desarrollo de un sistema IoT que integra sensores f√≠sicos con una arquitectura de comunicaci√≥n basada en MQTT, dise√±ada para recolectar, analizar y transmitir en tiempo real datos sobre temperatura, presencia de gas y detecci√≥n de llama en los cerros orientales de Bogot√°.
</p>

<p align="justify">
La informaci√≥n capturada se visualiza localmente a trav√©s de un servidor web embebido en el ESP32 y, simult√°neamente, se env√≠a a una Raspberry Pi que act√∫a como IoT Gateway. All√≠, los datos se almacenan en una base de datos SQLite y se reenv√≠an a la plataforma IoT Ubidots. Esta plataforma permite la consulta remota desde cualquier navegador web, la recepci√≥n de alertas y la ejecuci√≥n de acciones como el apagado de alarmas f√≠sicas mediante un bot√≥n interactivo. De esta forma, se garantiza un monitoreo constante, una trazabilidad confiable de los datos recolectados y una capacidad de respuesta r√°pida y descentralizada ante posibles emergencias ambientales; de esta manera, contribuyendo para optimizar los tiempos de respuesta, reducir el impacto ambiental de los incendios y fortalecer las estrategias de prevenci√≥n.
</p>

---

## 2. Motivaci√≥n y Justificaci√≥n
<p align="justify">
Actualmente, la detecci√≥n de incendios en los cerros orientales de Bogot√° depende principalmente de vigilancia manual o reportes ciudadanos, lo que retrasa la respuesta de las autoridades y agrava los da√±os ecol√≥gicos, sociales y econ√≥micos. Ante este contexto, surge la necesidad de implementar soluciones tecnol√≥gicas que permitan un monitoreo constante, preciso y en tiempo real, capaces de generar alertas autom√°ticas y accesibles desde cualquier punto del pa√≠s.
</p>

<p align="justify">
Este proyecto propone una soluci√≥n basada en Internet de las Cosas (IoT) que integra un sensor de temperatura (DS18B20), un sensor de llama y un sensor de gas (MQ-2), conectados a un ESP32 que recolecta los datos y activa alarmas visuales y sonoras, incluyendo un zumbador activo, un LED RGB y un m√≥dulo LCD I2C, para generar notificaciones en tiempo real en caso de riesgo. La informaci√≥n capturada se transmite mediante el protocolo de comunicaci√≥n MQTT a una Raspberry Pi, que act√∫a como IoT Gateway, donde se almacenan los datos en una base de datos SQLite y se reenv√≠an a la plataforma IoT Ubidots. Desde all√≠, las autoridades pueden visualizar remotamente los datos desde cualquier navegador web o dispositivo m√≥vil, adem√°s, de recibir y desactivar las alertas fisicas automatizadas.
</p>

<p align="justify">
Gracias a su arquitectura modular, al uso de comunicaci√≥n inal√°mbrica y a su capacidad de respuesta autom√°tica, esta soluci√≥n mejora significativamente los tiempos de reacci√≥n ante emergencias, facilita el seguimiento preciso de las condiciones ambientales y contribuye al fortalecimiento de estrategias prevenci√≥n y mitigaci√≥n de incendios forestales en zonas vulnerables y de dif√≠cil acceso.
</p>

---

## 3. Soluci√≥n Propuesta

### Restricciones de Dise√±o Identificadas

<p align="justify"> 
Al desarrollar el sistema IoT para detectar incendios en los cerros orientales de Bogot√°, se identificaron varias restricciones que afectan su dise√±o, implementaci√≥n y comunicaci√≥n de datos:
</p> 

#### 1. T√©cnicas
- Se usa un **ESP32**, el cual tiene un l√≠mite en la memoria RAM y el procesamiento, lo que puede afectar la ejecuci√≥n simult√°nea del servidor web local, la recolecci√≥n de datos de sensores, la transmisi√≥n de datos mediante MQTT hacia el broker en la Raspberry Pi.
- Los sensores de **temperatura, gas (MQ-2) y llama** requieren una calibraci√≥n precisa para minimizar falsas alarmas, adem√°s de que tienen determinados tiempos de respuesta que pueden influir en la detecci√≥n temprana de incendios.
- Dependencia de una conexi√≥n estable de WiFi para enviar y visualizar los datos en la interfaz web local, mantener la conexi√≥n activa con el broker MQTT y permitir la actualizaci√≥n de estados en tiempo real hacia Ubidots.
- Para la gesti√≥n de m√∫ltiples tareas, se implement√≥ un enfoque optimizado que permite la adquisici√≥n de datos sin afectar la estabilidad del sistema y la transmisi√≥n de datos por MQTT.
- El sistema debe gestionar reconexiones autom√°ticas al broker MQTT para garantizar continuidad en la transmisi√≥n de datos ante ca√≠das de red.
- La Raspberry Pi debe estar configurada de forma robusta para funcionar como un broker MQTT estable, lo que incluye la gesti√≥n de m√∫ltiples conexiones simult√°neas si se escala el sistema.
- Dependencia de servicios en la nube (Ubidots), lo que introduce una nueva capa de posibles fallas externas (ca√≠das del servicio, latencia en internet).

#### 2. Econ√≥micas
- Se sigue buscando minimizar costos, utilizando hardware como el ESP32, sensores econ√≥micos pero siendo lo suficientemente confiables y precisos para realizar apropiadamente las mediciones; y una Raspberry Pi como broker local en lugar de servicios en la nube de pago (por ejemplo, brokers MQTT comerciales).
- Utilizaci√≥n del Free Trial de Ubidots para el monitoreo en la nube.
- uso de tecnolog√≠as inal√°mbricas evita la necesidad de instalaciones f√≠sicas costosas (cableados extensivos, redes cableadas).
  
#### 3. Regulatorias
- Se debe cumplir con normativas ambientales y est√°ndares de seguridad el√©ctrica para su instalaci√≥n de equipos electr√≥nicos en zonas protegidas y transmisi√≥n de datos desde dispositivos de monitoreo ambiental.
- Cualquier intervenci√≥n en los cerros debe ajustarse a regulaciones locales.
- Debe revisarse el cumplimiento de normas sobre protecci√≥n de datos y transmisi√≥n segura con plataformas externas (Ubidots).

#### 4. Espaciales
- El sistema debe ser compacto y resistente a condiciones clim√°ticas adversas (lluvia, humedad y polvo).
- Los sensores deben estar ubicados estrat√©gicamente para detectar cambios de temperatura y gases sin interferencias, maximizando su efectividad sin afectar el ecosistema.
- Para el montaje del sistema, la integraci√≥n de la pantalla LCD, los LEDs y los sensores debe ser compacta y accesible.

#### 5. Escalabilidad
- Aunque es un prototipo, debe permitir mejoras o expansi√≥n en el futuro como agregar m√°s nodos ESP32 reportando al mismo broker Raspberry Pi y hacer que Ubidots permite visualizar m√∫ltiples dispositivos o variables en un mismo tablero de control. 
- Si se desean conectar m√∫ltiples dispositivos a un mismo servidor, se debe optimizar la comunicaci√≥n para no saturar la red.
- Si se requiere monitoreo en varias √°reas, se debe considerar el crecimiento del n√∫mero de conexiones simult√°neas al broker local y el impacto en el desempe√±o de la red WiFi.

#### 6. Temporales
- El sistema debe operar en **tiempo real** para detectar incendios lo m√°s r√°pido posible, donde se establece un intervalo adecuado entre lecturas en los sensores para evitar saturaci√≥n del sistema sin comprometer la detecci√≥n temprana (3 segundos), en este caso para la publicaci√≥n de datos MQTT y que el servidor local y la nube muestren las actualizaciones casi inmediatamente despu√©s de cada lectura.
- El sistema ahora requiere mecanismos de reconexi√≥n autom√°tica para el broker MQTT y para el WiFi, minimizando la necesidad de intervenci√≥n manual constante.
  
### Arquitectura Propuesta

<p align="justify"> 
A continuaci√≥n, se presenta un Diagrama de Bloques que ilustra los elementos de hardware y software que conforman la soluci√≥n IoT desarrollada.
</p> 

![Diagrama de la arquitectura de la soluci√≥n](Diagramas/DiagramaBloques.png)
*Figura 1: Arquitectura IoT propuesta de la soluci√≥n.*

<p align="justify"> 
El sistema est√° basado en un ESP32, que act√∫a como la unidad de procesamiento principal y se encarga de la adquisici√≥n y preprocesamiento de datos provenientes de los sensores de temperatura (DS18B20), gas (MQ-2) y llama. El m√≥dulo de sensado opera de forma continua en segundo plano, y la informaci√≥n recopilada es analizada localmente para actualizar el historial de temperaturas, detectar condiciones de riesgo y gestionar alertas en tiempo real. En caso de que se determine una situaci√≥n de peligro, el m√≥dulo de actuadores entra en acci√≥n, activando un LED RGB y un buzzer para alertar sobre el estado del ambiente. Asimismo, el m√≥dulo de visualizaci√≥n local actualiza la pantalla LCD I2C, donde se muestra en tiempo real la temperatura y el estado general del entorno. </p>

<p align="justify">
De igual forma, con respecto a la visualizaci√≥n local, el ESP32 implementa un servidor web embebido (EWS), permitiendo a los usuarios monitorear el sistema en tiempo real a trav√©s de una interfaz web accesible desde cualquier dispositivo conectado a la misma red WiFi, la cual puede establecerse utilizando un punto de acceso externo (AP o router). La comunicaci√≥n entre el ESP32 y los navegadores web se realiza mediante el protocolo TCP/IP. </p>

<p align="justify"> En cuanto a la comunicaci√≥n externa, el ESP32 utiliza el protocolo MQTT para enviar peri√≥dicamente los datos adquiridos a una Raspberry Pi configurada como Gateway IoT. Esta Raspberry Pi alberga un broker Mosquitto y una base de datos SQLite que permite el almacenamiento local de la informaci√≥n, proporcionando una capa adicional de persistencia y respaldo de datos. </p>

<p align="justify"> Adicionalmente, la Raspberry Pi se encarga de reenviar los datos relevantes a la plataforma en la nube Ubidots, tambi√©n utilizando el protocolo MQTT. Desde Ubidots, se habilita la supervisi√≥n remota del sistema y el control de alarmas, permitiendo el env√≠o de comandos de respuesta hacia el ESP32 en situaciones de emergencia o condiciones an√≥malas. </p>

<p align="justify">
Cabe resaltar que toda la l√≥gica de adquisici√≥n, procesamiento, comunicaci√≥n y control ha sido desarrollada en C++ para el ESP32 y Python para la Raspberry Pi, garantizando una operaci√≥n eficiente, aut√≥noma y resiliente del sistema IoT propuesto. Esta integraci√≥n de hardware y software permite monitorear el entorno de forma local y remota, maximizando la confiabilidad y accesibilidad de la soluci√≥n. </p>


### Desarrollo Te√≥rico Modular: Criterios de Dise√±o Establecidos
Para que el sistema sea eficiente y funcional, se definieron los siguientes criterios de dise√±o:

#### 1. Fiabilidad y Precisi√≥n
- Se seleccionaron sensores adecuados para la detecci√≥n confiable sobre temperatura, gases y llamas.
- Se implementaron l√≠mites y filtros dentro del c√≥digo para reducir errores y evitar falsas alarmas.
- Se optimiz√≥ la adquisici√≥n de datos mediante procesamiento concurrente, asegurando mediciones en tiempo real.

#### 2. Autonom√≠a y Eficiencia
- El sistema es **autosuficiente**, operando sin necesidad de conexi√≥n a redes externas para su funcionamiento b√°sico local.
- Su dise√±o es resistente a la intemperie, minimizando la necesidad de mantenimiento.
- Se implementaron mecanismos de gesti√≥n de tareas para evitar interrupciones en la adquisici√≥n de datos y la respuesta del sistema.
  - Se implement√≥ **FreeRTOS**, lo que permiti√≥ la creaci√≥n de un segundo y tercer hilo, mejorando la gesti√≥n del tiempo de ejecuci√≥n de las tareas.
- **Concurrencia en FreeRTOS:** La concurrencia permite que varias tareas se ejecuten de manera que parecen realizarse simult√°neamente.
  - Con **FreeRTOS**, se logr√≥ una gesti√≥n real de tareas en paralelo mediante m√∫ltiples hilos de ejecuci√≥n.

#### 3. Interfaz de Usuario Intuitiva
- Se usa una **pantalla LCD** para mostrar datos en tiempo real localmente.
- Se integr√≥ un servidor web embebido (EWS) con HTML y JavaScript para la visualizaci√≥n remota a nivel de red local.
- Se incorporaron gr√°ficos din√°micos con historial de mediciones recientes en la interfaz web.
- Se implementaron **alarmas visuales (LED RGB) y sonoras (zumbador)** para notificaciones inmediatas sobre condiciones de riesgo.
- Adicionalmente, se habilit√≥ la monitorizaci√≥n remota a trav√©s de la nube utilizando la plataforma Ubidots, proporcionando acceso a la informaci√≥n desde cualquier lugar con conexi√≥n a Internet.

#### 4. Escalabilidad y Modularidad
- El sistema fue dise√±ado de forma **modular**, permitiendo agregar nuevos sensores o funciones en el futuro de manera sencilla.
- Se implement√≥ el protocolo **MQTT** como capa de comunicaci√≥n entre el dispositivo ESP32 y un Gateway IoT (Raspberry Pi).
- La Raspberry Pi no solo recibe y almacena datos localmente utilizando SQLite, sino que tambi√©n act√∫a como puente para transmitir los datos a la nube (Ubidots) mediante MQTT.

#### 5. Persistencia de Datos y Resiliencia
- Se implement√≥ un esquema de persistencia local mediante una base de datos SQLite en la Raspberry Pi, garantizando que los datos hist√≥ricos est√©n disponibles incluso en caso de p√©rdida temporal de conexi√≥n a Internet.
- La duplicidad de almacenamiento (local en la Raspberry Pi y en la nube con Ubidots) asegura la disponibilidad continua de la informaci√≥n cr√≠tica.


### Diagramas UML
<p align="justify"> 
Las siguientes figuras ilustran los Diagramas de Actividad UML de la soluci√≥n propuesta, donde se representan los flujos de ejecuci√≥n del sistema de monitoreo y alerta. Para una mejor visualizaci√≥n, se proporciona un enlace adicional:
</p>

![Diagrama de actividades de la ESP32](Diagramas/DiagramaActividadesESP32.png)
*Figura 2: Diagrama UML de actividades de la soluci√≥n propuesta - ESP32.*

![Diagrama de actividades de la Raspberry Pi](Diagramas/DiagramaActividadesRaspPi.png)
*Figura 3: Diagrama UML de actividades de la soluci√≥n propuesta - Raspberry Pi.*

- ***Link para mejor visualizaci√≥n de los diagramas:*** [https://www.canva.com/design/DAGltZirFnE/EE1g_EzG_OeKU2u2RzFSxw/view?utm_content=DAGltZirFnE&utm_campaign=designshare&utm_medium=link2&utm_source=uniquelinks&utlId=h096d0f7c1d](https://www.canva.com/design/DAGltZirFnE/EE1g_EzG_OeKU2u2RzFSxw/view?utm_content=DAGltZirFnE&utm_campaign=designshare&utm_medium=link2&utm_source=uniquelinks&utlId=h096d0f7c1d)

<p align="justify"> 
Los diagramas representan el flujo de ejecuci√≥n de los dos componentes principales del sistema: el ESP32, encargado de la sensorizaci√≥n y del procesamiento, y la Raspberry Pi, responsable del almacenamiento y env√≠o de informaci√≥n hacia la nube. </p>

#### Flujo de Actividades en el ESP32

<p align="justify"> 
El ESP32 tiene la responsabilidad de capturar y procesar inicialmente los datos del entorno, as√≠ como de activar mecanismos de alerta en caso de detectar condiciones an√≥malas. Su flujo de ejecuci√≥n se organiza en los siguientes m√≥dulos principales: </p>

- **Entorno y flujo principal:** Configura los pines de entrada y salida, inicializa la pantalla LCD, los sensores (temperatura, gas y llama) y las variables globales de estado. Tambi√©n se inicia un servidor web local para permitir la interacci√≥n desde un navegador web.
- **M√≥dulo de comunicaci√≥n MQTT:** De forma general y en un segundo hilo, establece la conexi√≥n con el broker MQTT para publicar los datos de los sensores y recibir posibles comandos desde la nube.
- **M√≥dulo de sensado:** En un tercer hilo, realiza lecturas peri√≥dicas de los sensores (DS18B20, MQ-2, sensor de llama), obteniendo mediciones en tiempo real del entorno f√≠sico.
- **M√≥dulo de procesamiento:** Analiza los datos capturados utilizando reglas basadas en umbrales predefinidos, determinando si las condiciones son seguras o de alerta. Adem√°s, se registran los valores de temperatura y se encarga de respetar la duraci√≥n establecida para las alertas.
- **M√≥dulo de actuadores:** Si se detecta una alerta espec√≠fica o de incendio, se activa la alarma poniendo el LED RGB en color rojo (indicando nivel de riesgo) y activando el buzzer para generar una advertencia sonora. En caso contrario, se desactiva este √∫ltimo y el LED toma un color verde.
- **M√≥dulo de visualizaci√≥n:** Actualiza los datos en la pantalla LCD y en el cliente web local. Permite al usuario visualizar las condiciones en tiempo real y gestionar la desactivaci√≥n de la alarma si es necesario. Lo anterior se logra a partir de la recepci√≥n de solicitudes HTTP GET desde el navegador para actualizar datos y gestionar la alarma.

<p align="justify"> 
Finalmente, Este ciclo de captura, procesamiento y comunicaci√≥n se repite aproximadamente cada 1000 ms, asegurando un monitoreo constante y en tiempo real. </p>

#### Flujo de Actividades en la Raspberry Pi

<p align="justify"> La Raspberry Pi cumple la funci√≥n de centro de comunicaci√≥n externa, recopilando los datos provenientes del ESP32, almacen√°ndolos localmente en una base de datos y retransmiti√©ndolos hacia la nube. El flujo de ejecuci√≥n est√° compuesto por los siguientes m√≥dulos: </p>

- **Entorno y flujo principal:** Inicializa la configuraci√≥n de red, establece la conexi√≥n con el broker MQTT y crea o abre la base de datos local (SQLite) para el almacenamiento de registros hist√≥ricos.
- **M√≥dulo de comunicaci√≥n MQTT:** Se suscribe a los t√≥picos relevantes para recibir los datos enviados por el ESP32, y puede publicar comandos de control (por ejemplo, para apagar alarmas).
- **M√≥dulo de procesamiento:** Parsea la informaci√≥n recibida en json desde el t√≥pico MQTT para poder separar y acceder a sus valores de temoeratura, gas, llama, alerta y mensaje.
- **M√≥dulo de persistencia de datos:** Guarda los datos de sensores junto con marcas de tiempo en la base de datos local, garantizando un respaldo hist√≥rico de la informaci√≥n recibida.
- **M√≥dulo de reenv√≠o a la nube:** Transfiere los datos procesados a plataformas de monitoreo remoto como Ubidots, asegurando que los usuarios puedan visualizar informaci√≥n en tiempo real desde cualquier ubicaci√≥n.
- **M√≥dulo de control de alarma remota:** Permite enviar comandos desde la nube para desactivar la alarma f√≠sica conectada al ESP32, cerrando as√≠ el ciclo de control remoto del sistema.

<p align="justify"> La colaboraci√≥n entre el ESP32 y la Raspberry Pi permite una soluci√≥n de monitoreo robusta, que no solo detecta eventos de riesgo en tiempo real, sino que tambi√©n ofrece almacenamiento de datos, visualizaci√≥n remota y control de alarmas a trav√©s de la nube. </p>


### Esquem√°tico de Hardware

### Est√°ndares de Ingenier√≠a Aplicados

---

## 4. Configuraci√≥n Experimental, Resultados y An√°lisis
<p align="justify">
  
</p>

---

## 5. Autoevaluaci√≥n del Protocolo de Pruebas
<p align="justify">

</p>

### Mejoras Identificadas en el Proceso de Pruebas

### Comparaci√≥n de Desempe√±o con Expectativas Iniciales
<p align="justify">

</p>

---

## 6. Conclusiones, Retos y Mejoras Futuras

### Conclusiones

### Retos Presentados Durante el Desarrollo del Proyecto

### Trabajo Futuro

---

## 7. Referencias

## 8. Anexos

### C√≥digo fuente de ESP32:
```
#include <WiFi.h>
#include <WebServer.h>
#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include <OneWire.h>
#include <DallasTemperature.h>
#include <freertos/FreeRTOS.h>
#include <freertos/task.h>
#include <PubSubClient.h>
#include <ArduinoJson.h>

// ========================== HTML PARA EL TABLERO ==========================
const char* htmlPage = R"rawliteral(
  <!DOCTYPE html>
  <html lang="es">
  <head>
      <meta charset="UTF-8">
      <meta name="viewport" content="width=device-width, initial-scale=1.0">
      <title>Monitor de Incendios</title>
      <script src="https://cdn.jsdelivr.net/npm/chart.js@3.7.1/dist/chart.min.js"></script>
      <style>
          body {
              font-family: Arial, sans-serif;
              text-align: center;
              background-color: #f4f4f4;
              margin: 0;
              padding: 0;
          }
          .container {
              text-align: center;
              max-width: 100%;
              margin: 20px auto;
              padding: 20px;
              background: white;
              border-radius: 10px;
              box-shadow: 0 0 10px rgba(0, 0, 0, 0.1);
          }
          h1 {
              color: #333;
          }
          .status {
              font-size: 20px;
              margin: 10px 0;
          }
          .alert {
              color: red;
              font-weight: bold;
          }
          .safe {
              color: green;
          }
          .button {
              display: inline-block;
              padding: 10px 20px;
              margin-top: 20px;
              font-size: 16px;
              color: white;
              background-color: #007BFF;
              border: none;
              border-radius: 5px;
              cursor: pointer;
          }
          .button:hover {
              background-color: #0056b3;
          }
          table {
              width: 100%;
              max-width: 600px; // Ajusta seg√∫n el tama√±o que desees 
              margin: auto; // Centra la tabla
              border-collapse: collapse;
          } 
          th, td {
              padding: 10px;
              text-align: center;
              border-bottom: 1px solid #ddd;
          }
          thead {
              color: white;
              background-color: #007BFF;
          }
      </style>
  </head>
  <body>
  <div class="container">
      <h1>Monitor de Incendios - Cerro Oriental</h1>
      <p id="alerta" class="status safe">Cargando...</p>
      <p class="status"><strong>Temperatura:</strong> <span id="temp">--</span></p>
      <p class="status"><strong>Gas:</strong> <span id="gas">--</span></p>
      <p class="status"><strong>Llama:</strong> <span id="llama">--</span></p>
      <button class="button" onclick="resetAlarm()">Apagar Alarma</button>
  </div>
  <div class="container">
      <h2>Historial de Mediciones</h2>
      <table border="1">
          <thead>
              <tr>
                  <th>Tiempo</th>
                  <th>Temperatura (¬∞C)</th>
                  <th>Gas</th>
                  <th>Llama</th>
              </tr>
          </thead>
          <tbody id="historial-body">
              <!-- Aqu√≠ se insertar√°n las filas din√°micamente -->
          </tbody>
      </table>
  </div>
  <script type="text/javascript" src="/script.js"></script>
  </body>
  </html>
  )rawliteral";

// ========================== CONFIGURACI√ìN WiFi ==========================
const char* ssid = "iPhone Ana (5)";   // Cambia esto por el nombre del hotspot del iPhone
const char* password = "Toby3333";  // Cambia esto por tu contrase√±a

WebServer server(80);

SemaphoreHandle_t dataMutex;

const char* mqtt_server = "raspberrypi.local";  // IP de Raspberry Pi
WiFiClient espClient;
PubSubClient client(espClient);

const char* mqtt_topic = "sensores/datos";

// C√≥digo JavaScript incrustado en una cadena
const char script_js[] PROGMEM = R"rawliteral(// Variable global para almacenar el historial de datos
  let historial = [];

  function fetchData() {
    fetch('/data')
        .then(response => response.json())
        .then(data => {
            // Actualizar valores en la interfaz
            document.getElementById("temp").innerText = data.temperatura + "¬∞C";
            document.getElementById("gas").innerText = data.gas;
            document.getElementById("llama").innerText = data.llama ? "üî• Detectada" : "‚úÖ No detectada";

            // Usar el mensaje real del LCD en la alerta
            let alertaElem = document.getElementById("alerta");
            alertaElem.innerText = data.mensaje;

            // Cambiar estilo visual de la alerta
            if (data.alerta) {
                alertaElem.classList.add("alert");
                alertaElem.classList.remove("safe");
            } else {
                alertaElem.classList.add("safe");
                alertaElem.classList.remove("alert");
            }

            // Guardar en historial y actualizar la tabla
            agregarRegistroHistorial(data);
        })
        .catch(error => console.error("Error obteniendo datos:", error));
}

function agregarRegistroHistorial(data) {
    // Obtener la hora actual
    let hora = new Date().toLocaleTimeString();
    
    // Agregar nuevo registro al historial
    historial.push({
        tiempo: hora,
        temperatura: data.temperatura,
        gas: data.gas,
        llama: data.llama ? "üî• Detectada" : "‚úÖ No detectada"
    });

    // Limitar a los √∫ltimos 10 registros para que no se desborde la tabla
    if (historial.length > 10) {
        historial.shift();
    }

    // Actualizar la tabla
    actualizarTabla();
}

function actualizarTabla() {
    let tabla = document.getElementById("historial-body");
    tabla.innerHTML = ""; // Limpiar la tabla antes de actualizar

    historial.forEach(registro => {
        let fila = `<tr>
            <td>${registro.tiempo}</td>
            <td>${registro.temperatura}¬∞C</td>
            <td>${registro.gas}</td>
            <td>${registro.llama}</td>
        </tr>`;
        tabla.innerHTML = fila + tabla.innerHTML;
    });
}

function resetAlarm() {
        console.log("Enviando solicitud para apagar la alarma...");
        fetch('/reset_alarm')
            .then(response => response.text())
            .then(() => {
                console.log("Alarma apagada correctamente");
                document.getElementById("alerta").innerText = "Estado Normal";
                document.getElementById("alerta").classList.add("safe");
                document.getElementById("alerta").classList.remove("alert");
            })
            .catch(error => {
                console.error("Error al resetear la alarma:", error);
            });
}

// Cargar datos al inicio y actualizar cada 5 segundos
window.onload = fetchData;
setInterval(fetchData, 5000);
)rawliteral";

// Manejar la petici√≥n del archivo script.js
void handleScriptJS() {
  server.send(200, "application/javascript", script_js);
}

// ========================== DEFINICI√ìN DE PINES ==========================
// Pines de los sensores
#define TEMP_SENSOR_PIN 15   // Pin digital para el sensor de temperatura 
#define FLAME_SENSOR_PIN 4   // Pin digital para el sensor de llama
#define GAS_SENSOR_PIN 35    // Pin anal√≥gico para el sensor de gas MQ-2

// Pines de los actuadores
#define BUZZER_SENSOR_PIN 18 // Pin digital para el zumbador
#define LED_RED_PIN 5        // Pin digital para componente rojo del LED RGB
#define LED_GREEN_PIN 19     // Pin digital para componente verde del LED RGB

#define SDA_PIN 22           // Pin digital para el SDA de la pantalla LCD I2C
#define SCL_PIN 23           // Pin digital para el SCL de la pantalla LCD I2C

// Configuraci√≥n de la pantalla LCD I2C
LiquidCrystal_I2C lcd(0x27, 16, 2);

// Configuraci√≥n del sensor de temperatura
OneWire oneWire(TEMP_SENSOR_PIN);
DallasTemperature sensors(&oneWire);


// ========================== PAR√ÅMETROS DEL SISTEMA ==========================
#define MAX_TEMPS 5  // N√∫mero de muestras de temperatura almacenadas

// Variables de almacenamiento de valores de sensores
float temperature;
int flameState;
int gasValue;
unsigned long tiempoActual;

// Umbrales de alerta
const float TEMP_LIMITE = 27;                  // Temperatura m√°xima permitida antes de alerta
const float TEMP_AUMENTO_RAPIDO = 0.5;           // Incremento r√°pido de temperatura en ¬∞C
const int GAS_LIMITE = 3200;                     // L√≠mite de concentraci√≥n de gas

const unsigned long DURACION_ALERTA = 3000;      // Duraci√≥n de alerta est√°ndar (ms)
const unsigned long DURACION_INCENDIO = 8000;    // Duraci√≥n de alerta por incendio (ms)
const unsigned long INTERVALO_TIEMPO = 10000;    // Duraci√≥n entre cada toma de medida para la temperatura

// Variables de almacenamiento de temperatura
float temperaturas[MAX_TEMPS] = {20.0, 20.0, 20.0, 20.0, 20.0};   // Inicializa con 20¬∞C
int indice = 0;                     // √çndice de la pr√≥xima lectura a almacenar
float temperaturaAnterior = 20.0;   // Inicializa con 20¬∞C

// Variables de tiempo para alertas
unsigned long tiempoUltimaMedicion = 0;     // Guarda el tiempo de la √∫ltima medicion de la temperatura
unsigned long tiempoAlerta = 0;             // Guarda el tiempo de inicio de la alerta
unsigned long tiempoIncendio = 0;           // Guarda el tiempo de inicio de la alerta de incendio
unsigned long tiempoAlarmaDesactivada = 0;  // Guarda el tiempo en el que se desactiv√≥ la alarma manualmente
const unsigned long tiempoEspera = 10000;   // 10 segundos de gracia para reactivar la alarma

bool alerta = false;                // Inicializa estado de alerta
bool esIncendio = false;            // Inicializa estado de alerta incendio
String mensaje = "Estado Normal";   // Inicializa mensaje de alerta del LCD con estado normal
bool alarmaManualOff = false;       // Falso por defecto, significa que no ha sido apagada manualmente

// ========================== LECTURA DE SENSORES ==========================
// Captura los valores de los sensores de temperatura, llama y gas
void leerSensores() {
  sensors.requestTemperatures();
  temperature = sensors.getTempCByIndex(0);    // Leer temperatura
  flameState = digitalRead(FLAME_SENSOR_PIN);  // Leer estado del sensor de llama (0 indica llama detectada)
  gasValue = analogRead(GAS_SENSOR_PIN);       // Leer nivel del sensor de gas
}

// ========================== MANEJO DE HISTORIAL DE TEMPERATURAS ==========================
// Actualiza los valores que hacen parte del registro de temperaturas
void actualizarHistorialTemperaturas() {
  temperaturas[indice] = temperature;
  indice = (indice + 1) % MAX_TEMPS;
}

// ========================== MANEJO DE ALERTAS ==========================
// Analiza los valores de los sensores y determina si se activa una alerta
void manejarAlertas() { 
  float incremento = temperature - temperaturaAnterior; // Comparaci√≥n con la medici√≥n de hace 10 segundos

  // Si han pasado 10 segundos, actualizamos la referencia de temperatura
  if (tiempoActual - tiempoUltimaMedicion >= INTERVALO_TIEMPO) {
    temperaturaAnterior = temperature;
    tiempoUltimaMedicion = tiempoActual;
  }

  // Evaluar condiciones individuales
  bool tempAlta = (incremento >= TEMP_AUMENTO_RAPIDO || temperature >= TEMP_LIMITE);
  bool llamaDetectada = (flameState == LOW);
  bool gasDetectado = (gasValue > GAS_LIMITE);

  bool enTiempoGracia = alarmaManualOff && (tiempoActual - tiempoAlarmaDesactivada < tiempoEspera);

  // Si estamos en tiempo de gracia, forzar salir
  if (enTiempoGracia) {
    alerta = false;
    return;
  }

  // Evaluar si se debe activar la alerta
  bool nuevaAlerta = tempAlta || llamaDetectada || gasDetectado;
  bool dentroDeDuracion = (tiempoActual - tiempoAlerta < DURACION_ALERTA) ||
                          (esIncendio && tiempoActual - tiempoIncendio < DURACION_INCENDIO);

  if (nuevaAlerta || dentroDeDuracion) {
      alerta = true;
      tiempoAlerta = nuevaAlerta ? tiempoActual : tiempoAlerta;
      esIncendio = (llamaDetectada && gasDetectado) || (llamaDetectada && tempAlta);

      if (esIncendio) {
          tiempoIncendio = tiempoActual;
          mensaje = "ALERTA: INCENDIO";
      } else if (tempAlta) {
          mensaje = "ALERTA: Temp alta!";
      } else if (llamaDetectada) {
          mensaje = "ALERTA: Llama!";
      } else if (gasDetectado) {
          mensaje = "ALERTA: Gas!";
      }
  } else {
      alerta = false;
  }
}

// ========================== ACTUALIZACI√ìN DEL LCD ==========================
// Muestra en la pantalla la temperatura y el estado actual del entorno
void actualizarLCD() {
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Temp:");
  lcd.print(temperature);
  lcd.print("C");

  lcd.setCursor(0, 1);
  lcd.print(mensaje);
}

// ========================== CONTROL DE ACTUADORES ==========================
// Activa o desactiva el LED RGB y el buzzer seg√∫n el estado del sistema
void manejarActuadores() {
  if (alerta && !alarmaManualOff) {
    digitalWrite(LED_RED_PIN, LOW);
    digitalWrite(LED_GREEN_PIN, HIGH);
    digitalWrite(BUZZER_SENSOR_PIN, LOW);
  } else {
    digitalWrite(LED_RED_PIN, HIGH);
    digitalWrite(LED_GREEN_PIN, LOW);
    digitalWrite(BUZZER_SENSOR_PIN, HIGH);
  }
}

// Funci√≥n para crear el JSON con los datos de los sensores
String generarJSON() {
  String json = "{";
  json += "\"temperatura\":" + String(temperaturas[(indice - 1 + MAX_TEMPS) % MAX_TEMPS]) + ",";
  json += "\"gas\":" + String(analogRead(GAS_SENSOR_PIN)) + ",";
  json += "\"llama\":" + String(digitalRead(FLAME_SENSOR_PIN) == LOW) + ",";
  json += "\"alerta\":" + String(alerta ? "true" : "false") + ",";
  json += "\"mensaje\":\"" + mensaje + "\"";
  json += "}";

  return json;
}

// ========================== TRANSMISION DE DATOS ==========================
// Reconectar con broker MQTT
void reconnectMQTT() {
  int attempt = 0;
  while (!client.connected() && attempt < 10) {
    Serial.print("Conectando al broker MQTT...");
    if (client.connect("ESP32Client")) {
      Serial.println("MQTT conectado");
      client.subscribe("esp32/reset_alarma");
    } else {
      Serial.print("Fall√≥ la conexi√≥n, rc=");
      Serial.print(client.state());
      Serial.println(" reintentando en 5 segundos");
      delay(5000);
      attempt++;
    }
  }
  if (attempt == 10) {
    Serial.println("No se pudo conectar despu√©s de 10 intentos.");
  }
}

// Publicar los datos de los sensores usando MQTT
void enviarDatosMQTT() {
  if (!client.connected()) {
    reconnectMQTT();
  }

  String payload = generarJSON();

  if (client.publish(mqtt_topic, payload.c_str())) {
    Serial.println("Mensaje enviado por MQTT: " + payload);
  } else {
    Serial.println("Error al enviar mensaje MQTT.");
  }
}

// Escuchar el mensaje del tablero de Ubidots sobre el bot√≥n de apagado en un callback
void callback(char* topic, byte* payload, unsigned int length) {
  String message;
  for (int i = 0; i < length; i++) {
    message += (char)payload[i];
  }

  Serial.print("Mensaje recibido [");
  Serial.print(topic);
  Serial.print("]: ");
  Serial.println(message);

  // Parsear el JSON recibido
  StaticJsonDocument<200> doc;
  DeserializationError error = deserializeJson(doc, message);

  if (error) {
    Serial.print("Error al parsear JSON: ");
    Serial.println(error.c_str());
    return;
  }

  float valorBoton = doc["value"];  // Extraer el valor del bot√≥n

  Serial.print("Valor del bot√≥n recibido: ");
  Serial.println(valorBoton);

  // Responder a bot√≥n manual Ubidots
  if (valorBoton == 1.0) {
    alarmaManualOff = true;  // Indicar que se apag√≥ manualmente
    tiempoAlarmaDesactivada = millis();  // Guardar el tiempo en que se desactiv√≥ la alarma
    alerta = false;  // Desactivar la alerta
    digitalWrite(BUZZER_SENSOR_PIN, HIGH);  // Apagar el buzzer (l√≥gica negativa)
    digitalWrite(LED_RED_PIN, HIGH);  // Apagar LED rojo
    digitalWrite(LED_GREEN_PIN, LOW); // Encender LED verde

    Serial.println("Alarma desactivada manualmente desde Ubidots.");
  } else {
    Serial.println("Valor distinto de 1.0 recibido.");
  }
}

// ========================== LECTURA DE SENSORES EN SEGUNDO HILO ==========================
// Funci√≥n de la tarea para leer los sensores peri√≥dicamente
void leerSensoresTask(void *pvParameters) {
  while (true) {
    if (xSemaphoreTake(dataMutex, portMAX_DELAY)) {
      leerSensores();
      tiempoActual = millis();
      xSemaphoreGive(dataMutex);
    }
    vTaskDelay(pdMS_TO_TICKS(1000)); // Espera 1000ms antes de la pr√≥xima lectura
  }
}


// ========================== TRANSMISI√ìN DE DATOS EN TERCER HILO ==========================
// Funci√≥n de la tarea para transmitir los datos en MQTT
void enviarDatosTask(void* parameter) {
  while (true) {
    if (xSemaphoreTake(dataMutex, portMAX_DELAY)) {
      enviarDatosMQTT();
      xSemaphoreGive(dataMutex);
    }
    vTaskDelay(pdMS_TO_TICKS(3000)); // Espera 3000ms antes de la pr√≥xima publicaci√≥n
  }
}

// ========================== CONFIGURACI√ìN INICIAL ==========================
void setup() {
  Serial.begin(115200);

  dataMutex = xSemaphoreCreateMutex(); // Crear un mutex (semaphore binario) global para proteger secciones cr√≠ticas

  // Conectar a WiFi
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.println("Conectando a WiFi...");
  }
  Serial.println("Conectado a WiFi");
  Serial.print("IP: ");
  Serial.println(WiFi.localIP());

  // Configuraci√≥n de pines
  pinMode(BUZZER_SENSOR_PIN, OUTPUT);
  pinMode(LED_RED_PIN, OUTPUT);
  pinMode(LED_GREEN_PIN, OUTPUT);
  pinMode(FLAME_SENSOR_PIN, INPUT);

  // Inicializaci√≥n de I2C y sensores
  Wire.begin(SDA_PIN, SCL_PIN);
  lcd.begin(16, 2);
  lcd.setBacklight(255);
  sensors.begin();

  // Mensaje de inicio en LCD
  lcd.setCursor(0, 0);
  lcd.print("Sistema Alerta");
  lcd.setCursor(0, 1);
  lcd.print("Iniciando...");
  delay(2000);
  lcd.clear();

  // ========================== SERVIDOR WEB ==========================
  // Ver el dashboard con gr√°ficos
  server.on("/", HTTP_GET, []() {
    server.send(200, "text/html", htmlPage);
  });

  // Servir el archivo JavaScript
  server.on("/script.js", HTTP_GET, handleScriptJS);

  // Acceder a los datos en formato JSON a trav√©s de la ruta /data
  server.on("/data", HTTP_GET, []() {
    String json = generarJSON();
    server.send(200, "application/json", json);
  });

  // Conectar al broker MQTT
  client.setServer(mqtt_server, 1883);
  client.setCallback(callback);
  reconnectMQTT();

  // Apagar la alarma desde la web
  server.on("/reset_alarm", HTTP_GET, []() {
    alarmaManualOff = true;  // Indicar que se apag√≥ manualmente
    tiempoAlarmaDesactivada = millis();  // Guardar el tiempo en que se desactiv√≥ la alarma
    alerta = false;  // Desactivar la alerta
    digitalWrite(BUZZER_SENSOR_PIN, HIGH);  // Apagar el buzzer (l√≥gica negativa)
    digitalWrite(LED_RED_PIN, HIGH);  // Apagar LED rojo
    digitalWrite(LED_GREEN_PIN, LOW); // Encender LED verde
    server.send(200, "text/html", "<h1>Alarma Apagada</h1><a href='/'>Volver</a>");
  });

  // ========================== CREAR SEGUNDO HILO PARA SENSORES ==========================
  xTaskCreate(
    leerSensoresTask, // Funci√≥n de la tarea
    "SensorTask",     // Nombre de la tarea
    4096,             // Tama√±o del stack
    NULL,             // Par√°metro de entrada
    1,                // Prioridad
    NULL              // Handle de la tarea
  );

  server.begin();  // Inicia servidor 

  // ========================== CREAR TERCER HILO PARA TRANSMISI√ìN ==========================
  xTaskCreate(
    enviarDatosTask,    // Funci√≥n de la tarea
    "TransmisionTask",  // Nombre de la tarea
    4096,               // Tama√±o del stack
    NULL,               // Par√°metro de entrada
    2,                  // Prioridad
    NULL                // Handle de la tarea
  );
}

// ========================== BUCLE PRINCIPAL ==========================
// Ejecuta la l√≥gica de monitoreo continuamente
void loop() {
  server.handleClient();
  client.loop();
  
  if (xSemaphoreTake(dataMutex, portMAX_DELAY)) {
    if (alarmaManualOff) {
      if (millis() - tiempoAlarmaDesactivada >= tiempoEspera) {
          alarmaManualOff = false;  // Permitir que la alarma se reactive despu√©s de 10 segundos
      }
    }

    actualizarHistorialTemperaturas();     // Actualizar el registro de temperaturas

    // Determinar si se debe activar una alerta
    manejarAlertas();

    // Respetar la duraci√≥n de alerta e incendio
    if (!alarmaManualOff && (millis() - tiempoAlerta < DURACION_ALERTA || millis() - tiempoIncendio < DURACION_INCENDIO)) {
      alerta = true;
      if (millis() - tiempoIncendio < DURACION_INCENDIO) {
        mensaje = "ALERTA: INCENDIO";
      }
    } else {
      // Si no estamos en alerta y no hay peligro, mostrar Estado Normal
      if (!alerta && !(temperature >= TEMP_LIMITE || flameState == LOW || gasValue > GAS_LIMITE)) {
        mensaje = "Estado Normal";
      }
    }

    actualizarLCD();        // Mostrar en LCD el estado actual
    manejarActuadores();    // Controlar los actuadores (LED y buzzer)
    xSemaphoreGive(dataMutex);
  }

  delay(100);  // Esperar 100ms antes de la pr√≥xima iteraci√≥n
}
```

### C√≥digo fuente de Raspberry Pi:
```
import paho.mqtt.client as mqtt
import sqlite3
import json
import time

# Conexi√≥n a base de datos SQLite (se crea si no existe)
db = sqlite3.connect("sensores_db", check_same_thread=False)
cursor = db.cursor()

# Asegurarse de que la tabla exista
cursor.execute('''
CREATE TABLE IF NOT EXISTS datos (
    id INTEGER PRIMARY KEY AUTOINCREMENT,
    timestamp DATETIME DEFAULT CURRENT_TIMESTAMP,
    temperatura REAL,
    gas INTEGER,
    llama BOOLEAN,
    alerta BOOLEAN,
    mensaje TEXT
)
''')
db.commit()

# Token Ubidots
UBIDOTS_TOKEN = "BBUS-rNyGEgYnILjIm80p8uX89MqlfoNKRu"
UBIDOTS_DEVICE = "raspberry-sensor-node"
UBIDOTS_TOPIC = f"/v1.6/devices/{UBIDOTS_DEVICE}"
UBIDOTS_RESET_TOPIC = f"/v1.6/devices/{UBIDOTS_DEVICE}/reset_alarma"

# MQTT cliente para enviar datos a Ubidots
ubidots_client = mqtt.Client()

# Agregar callback para verificar conexi√≥n
def on_connect(client, userdata, flags, rc):
    if rc == 0:
        print("‚úÖ Conectado correctamente a Ubidots")
    else:
        print("‚ùå Error de conexi√≥n a Ubidots. C√≥digo:", rc)

ubidots_client.on_connect = on_connect
ubidots_client.username_pw_set(UBIDOTS_TOKEN, "")
ubidots_client.connect("industrial.api.ubidots.com", 1883)
ubidots_client.loop_start()  # ‚úÖ Necesario para mantener conexi√≥n

# MQTT cliente para comunicarse con ESP32
local_broker_client = mqtt.Client()
local_broker_client.connect("localhost", 1883)

# CALLBACK: cuando llega un mensaje desde ESP32 (datos de sensores)
def on_esp32_message(client, userdata, message):
    try:
        payload = json.loads(message.payload.decode())
        print("[ESP32 ‚ûú Raspberry] Datos recibidos: ", payload)

        temperatura = payload["temperatura"]
        gas = payload["gas"]
        llama = payload["llama"]
        alerta = payload["alerta"]
        mensaje = payload["mensaje"]

        # Guardar en la base de datos local
        sql = '''INSERT INTO datos (temperatura, gas, llama, alerta, mensaje) 
                 VALUES (?, ?, ?, ?, ?)'''
        datos = (temperatura, gas, llama, alerta, mensaje)
        cursor.execute(sql, datos)
        db.commit()
        print("Guardado en DB local")

        # Reenviar a Ubidots por MQTT (solo campos v√°lidos num√©ricamente)
        ubidots_payload = json.dumps({
            "temperatura": temperatura,
            "gas": gas,
            "llama": {
                "value": int(llama),
                "context": {
                    "estado": "Detectada" if llama else "No detectada"
                }
            },
            "alerta": {
                "value": int(alerta),
                "context": {
                    "estado": "ALERTA" if alerta else "Normal"
                }
            },
            "mensaje": {
                "value": int(alerta),
                "context": {
                    "descripcion": mensaje
                }
            }
        })
        ubidots_client.publish(UBIDOTS_TOPIC, ubidots_payload)
        print("[Raspberry ‚ûú Ubidots] Datos enviados: ", ubidots_payload)

    except Exception as e:
        print("Error al procesar datos del ESP32:", e)

# CALLBACK: cuando llega un mensaje desde Ubidots (bot√≥n de reset remoto)
def on_ubidots_reset(client, userdata, message):
    try:
        payload = message.payload.decode()
        print("[Ubidots ‚ûú Raspberry] Bot√≥n recibido:", payload)

        # Reenviar al ESP32
        local_broker_client.publish("esp32/reset_alarma", payload)
        print("[Raspberry ‚ûú ESP32] Bot√≥n reenviado:", payload)

    except Exception as e:
        print("Error al reenviar bot√≥n:", e)

# Suscriptor del broker local (recibe del ESP32)
esp32_listener = mqtt.Client()
esp32_listener.on_message = on_esp32_message
esp32_listener.connect("localhost", 1883)
esp32_listener.subscribe("sensores/datos")

# Suscriptor del broker de Ubidots (recibe bot√≥n)
ubidots_listener = mqtt.Client()
ubidots_listener.username_pw_set(UBIDOTS_TOKEN, "")
ubidots_listener.on_message = on_ubidots_reset
ubidots_listener.connect("industrial.api.ubidots.com", 1883)
ubidots_listener.subscribe(UBIDOTS_RESET_TOPIC)

# Ejecutar bucles
esp32_listener.loop_start()
ubidots_listener.loop_forever()
```
